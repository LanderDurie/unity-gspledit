#define GROUP_SIZE 1024

#include "../../Gaussian/Compute/GaussianSplatting.hlsl"

#pragma kernel CSSelectionUpdate
#pragma kernel CSVertexTransform

RWStructuredBuffer<float3> _VertexModPos;
RWByteAddressBuffer _VertexSelectedBits;
RWByteAddressBuffer _VertexDeletedBits;
uint _VertexCount;
uint _SplatCount;
float4 _SelectionRect;
bool _SubtractMode;

float4x4 _MatrixObjectToWorld;
float4x4 _MatrixVP;
float4 _VecScreenParams;

[numthreads(GROUP_SIZE,1,1)]
void CSSelectionUpdate (uint3 id : SV_DispatchThreadID) {
    uint idx = id.x;
    if (idx >= _VertexCount)
        return;

    float3 pos = _VertexModPos[idx];

    float3 centerWorldPos = mul(_MatrixObjectToWorld, float4(pos,1)).xyz;
    float4 centerClipPos = mul(_MatrixVP, float4(centerWorldPos, 1));
    bool behindCam = centerClipPos.w <= 0;
    if (behindCam) {
        return;
    }

    uint wordIdx = idx / 32;
    uint bitIdx = idx & 31;

    // Return if vertex deleted
    uint selectionWord = _VertexDeletedBits.Load(wordIdx * 4);
    uint isBitSet = (selectionWord >> bitIdx) & 1;
    if (isBitSet) {
        return;
    }

    // Return if subtract mode and vertex not selected
    selectionWord = _VertexSelectedBits.Load(wordIdx * 4);
    isBitSet = (selectionWord >> bitIdx) & 1;
    if (_SubtractMode && !isBitSet) {
        _VertexSelectedBits.InterlockedAnd(wordIdx * 4, ~(1u << bitIdx)); // -
        return;
    }


    float2 pixelPos = (centerClipPos.xy / centerClipPos.w * float2(0.5, -0.5) + 0.5) * _VecScreenParams.xy;
    if (pixelPos.x < _SelectionRect.x || pixelPos.x > _SelectionRect.z ||
        pixelPos.y < _SelectionRect.y || pixelPos.y > _SelectionRect.w) {
        // remove is previously set but not in rect
        if (_SubtractMode && isBitSet) {
            _VertexSelectedBits.InterlockedAnd(wordIdx * 4, ~(1u << bitIdx)); // -
        }
        return;
    }

    _VertexSelectedBits.InterlockedOr(wordIdx * 4, 1u << bitIdx); // +
}


float3 _PositionDiff;
float4 _RotationDiff;
float3 _ScaleDiff;
float3 _PivotPoint; // pivot point for rotation and scale

float4x4 QuaternionToMatrix(float4 quat) {
    float x = quat.x;
    float y = quat.y;
    float z = quat.z;
    float w = quat.w;

    float x2 = x + x;
    float y2 = y + y;
    float z2 = z + z;
    float xx = x * x2;
    float xy = x * y2;
    float xz = x * z2;
    float yy = y * y2;
    float yz = y * z2;
    float zz = z * z2;
    float wx = w * x2;
    float wy = w * y2;
    float wz = w * z2;

    float4x4 mat;
    mat[0] = float4(1.0f - (yy + zz), xy - wz, xz + wy, 0.0f);
    mat[1] = float4(xy + wz, 1.0f - (xx + zz), yz - wx, 0.0f);
    mat[2] = float4(xz - wy, yz + wx, 1.0f - (xx + yy), 0.0f);
    mat[3] = float4(0.0f, 0.0f, 0.0f, 1.0f);

    return mat;
}

[numthreads(GROUP_SIZE, 1, 1)]
void CSVertexTransform(uint3 id : SV_DispatchThreadID) {
    uint idx = id.x;
    if (idx >= _VertexCount)
        return;

    uint wordIndex = idx >> 5;  
    uint bitPosition = idx & 31;
    uint selectionWord = _VertexSelectedBits.Load(wordIndex * 4);
    uint isBitSet = (selectionWord >> bitPosition) & 1;

    if (isBitSet) {
        float3 basePos = _VertexModPos[idx].xyz;

        float3 translatedPos = basePos + _PositionDiff;
        translatedPos -= _PivotPoint;

        // Apply rotation
        float4x4 rotationMatrix = QuaternionToMatrix(_RotationDiff);
        float3 rotatedPos = mul(rotationMatrix, float4(translatedPos, 1.0f)).xyz;
        rotatedPos += _PivotPoint;

        // Apply scaling relative to pivot
        float3 scaledPos = (rotatedPos - _PivotPoint) * (1.0f + _ScaleDiff);

        float3 finalPos = scaledPos + _PivotPoint;

        _VertexModPos[idx] = finalPos;
    }
}