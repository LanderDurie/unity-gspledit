#pragma kernel OpacityContributions
#include "../../../../Gaussian/Compute/GaussianSplatting.hlsl"

struct VertexProperties {
    float3 pos;
    float3 normal;
    uint colorIds[4];
    float3 posMod;
    float4 rotMod;
    float3 scaleMod;
    uint colorModIds[4];
};

struct TriangleProperties {
    int vertexId1;
    int vertexId2;
    int vertexId3;
};

struct SplatLinks {
    int triangleIds[8];
    float triangleWeights[8];
    float triangleX[8];
    float triangleY[8];
};

RWStructuredBuffer<VertexProperties> _VertexProps;
RWStructuredBuffer<TriangleProperties> _TriangleProps;
RWStructuredBuffer<SplatLinks> _SplatLinkBuffer;
uint _SplatCount;
uint _VertexCount;
uint _TriangleCount;
float _GlobalSigma;
static const float MAX_DISTANCE = 1e30f;
static const int MAX_LINKS = 8;

// Helper function to convert a 3x3 matrix to a 4x4 matrix
float4x4 Convert3x3To4x4(float3x3 mat3x3) {
    return float4x4(
        float4(mat3x3[0], 0.0f),
        float4(mat3x3[1], 0.0f),
        float4(mat3x3[2], 0.0f),
        float4(0.0f, 0.0f, 0.0f, 1.0f)
    );
}

// Function to convert a quaternion to a 4x4 rotation matrix
float4x4 quatToMatrix(float4 quat) {
    float3x3 rotationMat = float3x3(
        1.0f - 2.0f * (quat.y * quat.y + quat.z * quat.z),  2.0f * (quat.x * quat.y - quat.w * quat.z),  2.0f * (quat.x * quat.z + quat.w * quat.y),
        2.0f * (quat.x * quat.y + quat.w * quat.z),  1.0f - 2.0f * (quat.x * quat.x + quat.z * quat.z),  2.0f * (quat.y * quat.z - quat.w * quat.x),
        2.0f * (quat.x * quat.z - quat.w * quat.y),  2.0f * (quat.y * quat.z + quat.w * quat.x),  1.0f - 2.0f * (quat.x * quat.x + quat.y * quat.y)
    );
    return Convert3x3To4x4(rotationMat);
}

float3 mahalanobis_transform(float3 pos, SplatData splat) {
    // Translate to local space and apply rotation and scaling
    pos -= splat.pos;
    float4x4 rotationMatrix = quatToMatrix(splat.rot);
    float4x4 inverseRotation = transpose(rotationMatrix);
    pos = mul(inverseRotation, float4(pos, 0.0)).xyz;
    pos /= splat.scale;
    return pos;
}

float3 dist(float3 p1, float3 p2, float3 p3, float3 extPos) {
    // Compute vectors relative to p1
    float3 v0 = p2 - p1;
    float3 v1 = p3 - p1;
    float3 v2 = extPos - p1;

    // Compute dot products
    float d00 = dot(v0, v0);
    float d01 = dot(v0, v1);
    float d11 = dot(v1, v1);
    float d20 = dot(v2, v0);
    float d21 = dot(v2, v1);

    // Compute the denominator of the barycentric coordinates
    float denom = d00 * d11 - d01 * d01;

    // Compute barycentric coordinates with improved stability
    float v = (d11 * d20 - d01 * d21) / max(denom, 1e-6f);
    float w = (d00 * d21 - d01 * d20) / max(denom, 1e-6f);
    float u = 1.0f - v - w;

    // Clamp barycentric coordinates
    v = max(0.0f, v);
    w = max(0.0f, w);
    u = max(0.0f, u);

    float sum = u + v + w;
    u /= sum;
    v /= sum;
    w /= sum;

    // Compute the closest point in the triangle
    float3 closestPoint = u * p1 + v * p2 + w * p3;

    // Compute the shortest distance
    float distance = length(extPos - closestPoint);

    return float3(distance, v, w);
}

[numthreads(128, 1, 1)]
void OpacityContributions(uint3 id : SV_DispatchThreadID) {
    uint splatIndex = id.x;
    if (splatIndex >= _SplatCount) return;

    SplatData splat = LoadSplatData(splatIndex);

    float minDistances[MAX_LINKS] = { MAX_DISTANCE, MAX_DISTANCE, MAX_DISTANCE, MAX_DISTANCE, 
                                      MAX_DISTANCE, MAX_DISTANCE, MAX_DISTANCE, MAX_DISTANCE };
    float minX[MAX_LINKS] = { 0, 0, 0, 0, 0, 0, 0, 0 };
    float minY[MAX_LINKS] = { 0, 0, 0, 0, 0, 0, 0, 0 };
    int minIndices[MAX_LINKS] = { -1, -1, -1, -1, -1, -1, -1, -1 };

    // First pass: find closest triangles
    [loop]
    for (uint i = 0; i < _TriangleCount; i++) {
        float3 worldDistance = dist(
            _VertexProps[_TriangleProps[i].vertexId1].pos, 
            _VertexProps[_TriangleProps[i].vertexId2].pos, 
            _VertexProps[_TriangleProps[i].vertexId3].pos, 
            splat.pos);

        // Transform vertices to splat's local space
        float3 pos1 = (_VertexProps[_TriangleProps[i].vertexId1].pos);
        float3 pos2 = (_VertexProps[_TriangleProps[i].vertexId2].pos);
        float3 pos3 = (_VertexProps[_TriangleProps[i].vertexId3].pos);

        float3 localDistance = dist(pos1, pos2, pos3, splat.pos);

        // Insert the new distance and index into the sorted list
        for (int j = 0; j < MAX_LINKS; j++) {
            if (localDistance.x < minDistances[j]) {
                // Shift existing values down
                for (int k = MAX_LINKS - 1; k > j; k--) {
                    minDistances[k] = minDistances[k - 1];
                    minIndices[k] = minIndices[k - 1];
                    minX[k] = minX[k - 1];
                    minY[k] = minY[k - 1];
                }

                // Insert the new triangle
                minDistances[j] = localDistance.x;
                minIndices[j] = i;
                minX[j] = localDistance.y;
                minY[j] = localDistance.z;

                break;
            }
        }
    }

    // Calculate weights and store results
    SplatLinks splatLinks;
    float totalWeight = 0.0f;
    float sigmaSquared = _GlobalSigma * _GlobalSigma;
    float threshold = 0;

    [unroll]
    for (int j = 0; j < MAX_LINKS; j++) {
        float weight = exp(-0.5 * (minDistances[j] * minDistances[j]) / sigmaSquared);
        if (j == 0) {
        splatLinks.triangleIds[j] = (weight > threshold) ? minIndices[j] : -1;
        splatLinks.triangleWeights[j] = (weight > threshold) ? weight : 0;
        splatLinks.triangleX[j] = (weight > threshold) ? minX[j] : 0;
        splatLinks.triangleY[j] = (weight > threshold) ? minY[j] : 0;
        

        totalWeight += splatLinks.triangleWeights[j];
        } else {
            splatLinks.triangleIds[j] = -1;
            splatLinks.triangleWeights[j] = 0;
            splatLinks.triangleX[j] =  0;
            splatLinks.triangleY[j] =  0;
        }
    }

    // Normalize weights if total weight is non-zero
    if (totalWeight > 0) {
        [unroll]
        for (int k = 0; k < MAX_LINKS; k++) {
            splatLinks.triangleWeights[k] /= totalWeight;
        }
    }

    _SplatLinkBuffer[splatIndex] = splatLinks;
}